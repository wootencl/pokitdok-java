apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.pokitdok"
archivesBaseName = "pokitdok-java"
version = "0.8"
sourceCompatibility = 1.6
targetCompatibility = 1.6

defaultTasks "test"

repositories {
	mavenCentral()
	jcenter()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/java')
        }
        resources.srcDir file('src/test/resources')
    }
}

configurations {
    compile {
        transitive = true
    }

	runtime {
        extendsFrom compile, testCompile
    }

	testCompile {
    	extendsFrom compile
    	transitive = true
    }

	integrationTestCompile {
		extendsFrom testCompile
	}

	integrationTestRuntime {
		extendsFrom testRuntime
	}
}

dependencies {
	compile "org.apache.httpcomponents:httpclient:4.5.+"
	compile "com.googlecode.json-simple:json-simple:1.1.+"
	testCompile "commons-io:commons-io:2.4+"
	testCompile "org.mockito:mockito-core:1.+"
}

test {
	useJUnit {
		includeCategories 'com.pokitdok.tests.categories.UnitTests'
	}
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

integrationTest {
	useJUnit {
		includeCategories "com.pokitdok.tests.categories.IntegrationTests"
	}
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

if (hasProperty("sonatypeUsername") && hasProperty("sonatypePassword")) {
	// Sonatype Publishing Configurations
	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = "javadoc"
		from "build/docs/javadoc"
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}

	signing {
		sign configurations.archives
	}


	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}

				pom.project {
					name "pokitdok-java"
					packaging "jar"
					description "A Java client library for the PokitDok Platform APIs."
					url "https://github.com/pokitdok/pokitdok-java"

					scm {
						url 'scm:git@github.com:pokitdok/pokitdok-java.git'
						connection 'scm:git@github.com:pokitdok/pokitdok-java.git'
					}

					licenses {
						license {
							name "The MIT License (MIT)"
							url "http://opensource.org/licenses/MIT"
						}
					}

					developers {
						developer {
							name "PokitDok, Inc."
							email "platform@pokitdok.com"
						}
					}
				}
			}
		}
	}
}
